#===============================================================================
#
# Copyright (C) 2021 Istituto Italiano di Tecnologia (IIT)
#
# This software may be modified and distributed under the terms of the
# GPL-2+ license. See the accompanying LICENSE file for details.
#
#===============================================================================

set(SDK_VERSION 3.17.6)
# set(SDK_BUILD 3605)
# path has changed in the past. Go to https://www.forcedimension.com/software to check most recent
set(SDK_URL https://downloads.forcedimension.com/sdk/sdk-${SDK_VERSION}-linux-x86_64-gcc.tar.gz)
set(SDK_LOCAL ${CMAKE_CURRENT_SOURCE_DIR}/sdk-3.17.6-linux-x86_64-gcc.tar.gz)
set(SDK_HASH 2ce05f0a782417a4cf77ab9d0fef73a9603adeb9570b273683ccfc24f8c0696e3ae7d2e68cae6b55a550db932abe36e359873e65e52e5253c5ed781a7f219c87)

function(download_file url filename)
  message("Downloading Force Dimension SDK...")
  file(DOWNLOAD ${url} ${filename} SHOW_PROGRESS STATUS DOWNLOAD_STATUS)

  list(GET DOWNLOAD_STATUS 0 STATUS_CODE)
  list(GET DOWNLOAD_STATUS 1 ERROR_MESSAGE)

  if(${STATUS_CODE} EQUAL 0)
    message(STATUS "Download completed successfully!")
  else()
    message(FATAL_ERROR "Error occurred during download: ${ERROR_MESSAGE}")
  endif()
endfunction(download_file)

# Download the SDK if not available
if(NOT EXISTS ${SDK_LOCAL})
   download_file(${SDK_URL} ${SDK_LOCAL})
endif()

# Download the SDK if the file is available but wrong
file(SHA512 ${SDK_LOCAL} SDK_LOCAL_HASH)
if(NOT "${SDK_HASH}" STREQUAL "${SDK_LOCAL_HASH}")
   download_file(${SDK_URL} ${SDK_LOCAL})
endif()

# Extract the SDK
message("Extracting SDK archive: ${SDK_LOCAL}")
file(ARCHIVE_EXTRACT INPUT ${SDK_LOCAL} DESTINATION ${CMAKE_CURRENT_SOURCE_DIR})
message("Extraction finished.")

# Configure the actual executable
set(EXE_TARGET_NAME yarp-omega3-server)
set(THREADS_PREFER_PTHREAD_FLAG ON)

# Find required dependencies
find_package(Libusb1)
find_package(Threads REQUIRED)
find_package(YARP REQUIRED COMPONENTS idl_tools os sig)

# Header files
set(${EXE_TARGET_NAME}_HDR
    sdk-${SDK_VERSION}/include/dhdc.h
    sdk-${SDK_VERSION}/include/drdc.h
    include/Server.h
)

# Source files
set(${EXE_TARGET_NAME}_HDR
    src/Server.cpp
    src/main.cpp
)

# Generate thrift sources
set(${EXE_TARGET_NAME}_THRIFT_HDR thrift/server_idl.thrift)
yarp_add_idl(${EXE_TARGET_NAME}_THRIFT_SRC ${${EXE_TARGET_NAME}_THRIFT_HDR})

# Add the executable
add_executable(${EXE_TARGET_NAME} ${${EXE_TARGET_NAME}_HDR} ${${EXE_TARGET_NAME}_SRC} ${${EXE_TARGET_NAME}_THRIFT_SRC})

target_include_directories(${EXE_TARGET_NAME}
                           PRIVATE
                           ${Libusb1_INCLUDE_DIRS}
                           ${CMAKE_CURRENT_SOURCE_DIR}/sdk-${SDK_VERSION}/include
                           ${CMAKE_CURRENT_SOURCE_DIR}/include
)

target_link_libraries(${EXE_TARGET_NAME}
                      PRIVATE
                      Threads::Threads
                      YARP::YARP_init
                      YARP::YARP_os
                      YARP::YARP_sig
                      ${CMAKE_CURRENT_SOURCE_DIR}/sdk-${SDK_VERSION}/lib/release/lin-x86_64-gcc/libdrd.a
                      ${Libusb1_LIBRARIES}
)

install(TARGETS ${EXE_TARGET_NAME} DESTINATION bin)
